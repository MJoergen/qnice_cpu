vunit i_two_stage_fifo(two_stage_fifo(synthesis))
{
   -- Additional signals used during formal verification
   signal f_count : integer range 0 to 3 := 0;


   -- set all declarations to run on clk_i
   default clock is rising_edge(clk_i);


   -----------------------------
   -- ASSERTIONS ABOUT OUTPUTS
   -----------------------------

   -- FIFO must be empty after reset
   f_after_reset_empty : assert always {rst_i} |=> not m_valid_o;

   -- FIFO must be ready after reset
   f_after_reset_ready : assert always {rst_i} |=> s_ready_o;

   -- Output must be stable until accepted
   f_output_stable : assert always {m_valid_o and not m_ready_i and not rst_i} |=> {stable(m_valid_o) and stable(m_data_o)};

   -- Ready must be stable until new data
   f_ready_stable : assert always {s_ready_o and not s_valid_i and not rst_i} |=> {stable(s_ready_o)};

   -- Keep track of amount of data flowing into and out of the FIFO
   p_count : process (clk_i)
   begin
      if rising_edge(clk_i) then
         -- Data flowing in, but not out.
         if s_valid_i and s_ready_o and not (m_valid_o and m_ready_i) then
            f_count <= f_count + 1;
         end if;

         -- Data flowing out, but not in.
         if m_valid_o and m_ready_i and not (s_valid_i and s_ready_o) then
            f_count <= f_count - 1;
         end if;

         if rst_i then
            f_count <= 0;
         end if;
      end if;
   end process p_count;

   -- The FIFO size is limited to 2.
   f_size : assert always {0 <= f_count and f_count <= 2};

   -- The FIFO size is as expected
   f_fill : assert always {f_count = to_integer(s_fill_o)};


   ----------------------------------------------------
   -- FIFO ORDERING
   ----------------------------------------------------

   -- Control signals to determine if the values have entered and/or left the FIFO
   signal f_sampled_in_d1  : std_logic := '0';
   signal f_sampled_in_d2  : std_logic := '0';
   signal f_sampled_out_d1 : std_logic := '0';
   signal f_sampled_out_d2 : std_logic := '0';

   -- Make sure d2 enters after d1.
   f_fifo_ordering_in : assume always {f_sampled_in_d2} |-> {f_sampled_in_d1};

   -- Verify the FIFO preserves ordering, i.e. d2 leaves after d1.
   f_fifo_ordering_out : assert always {f_sampled_out_d2} |-> {f_sampled_out_d1};


   -- Two random values to be pushed into the FIFO
   signal f_value_d1 : std_logic_vector(G_DATA_SIZE - 1 downto 0);
   signal f_value_d2 : std_logic_vector(G_DATA_SIZE - 1 downto 0);
   attribute anyconst : boolean;
   attribute anyconst of f_value_d1 : signal is true;
   attribute anyconst of f_value_d2 : signal is true;
   assume f_value_d1 /= f_value_d2;

   p_sampled : process (clk_i)
   begin
      if rising_edge(clk_i) then
         if s_valid_i then
            if s_data_i = f_value_d1 then
               f_sampled_in_d1 <= '1';
            end if;
            if s_data_i = f_value_d2 then
               f_sampled_in_d2 <= '1';
            end if;
         end if;

         if m_valid_o then
            if m_data_o = f_value_d1 then
               f_sampled_out_d1 <= '1';
            end if;
            if m_data_o = f_value_d2 then
               f_sampled_out_d2 <= '1';
            end if;
         end if;

         if rst_i = '1' then
            f_sampled_in_d1  <= '0';
            f_sampled_in_d2  <= '0';
            f_sampled_out_d1 <= '0';
            f_sampled_out_d2 <= '0';
         end if;
      end if;
   end process p_sampled;


   -----------------------------
   -- ASSUMPTIONS ABOUT INPUTS
   -----------------------------

   -- Require reset at startup.
   f_reset : assume {rst_i};


   --------------------------------------------
   -- COVER STATEMENTS TO VERIFY REACHABILITY
   --------------------------------------------

   -- Make sure FIFO can transition from full to empty.
   f_full_to_empty : cover {f_count = 2; f_count = 1; f_count = 0};

} -- vunit i_two_stage_fifo(two_stage_fifo(synthesis))

