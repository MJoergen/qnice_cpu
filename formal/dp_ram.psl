vunit i_dp_ram(dp_ram(synthesis))
{

   -- set all declarations to run on clk
   default clock is rising_edge(clk_i);


   ----------------------------------------------
   -- INTERNAL ASSERTIONS
   ----------------------------------------------


   ----------------------------------------------
   -- ASSERTIONS ON OUTPUTS
   ----------------------------------------------

   signal c_addr : std_logic_vector(G_ADDR_SIZE-1 downto 0);
   attribute anyconst : boolean;
   attribute anyconst of c_addr : signal is true;

   signal f_data    : std_logic_vector(G_DATA_SIZE-1 downto 0);
   signal f_written : std_logic := '0';

   p_written : process (clk_i)
   begin
      if rising_edge(clk_i) then
         if wr_en_i = '1' and wr_addr_i = c_addr then
            f_data    <= wr_data_i;
            f_written <= '1';
         end if;
      end if;
   end process p_written;

   -- We can read back last value written to any given address
   f_read_back : assert always {f_written = '1' and rd_addr_i = c_addr and wr_en_i = '0' and rd_en_i = '1'} |=> {rd_data_o = f_data};

   -- Output value only changes when rd_en_i is asserted.
   f_stable : assert always {not rd_en_i} |=> {stable(rd_data_o)};


   ----------------------------------------------
   -- ASSUMPTIONS ABOUT INPUTS
   ----------------------------------------------

   signal f_falling_rd_en   : std_logic;
   signal f_falling_rd_addr : std_logic_vector(G_ADDR_SIZE-1 downto 0);

   p_falling : process (clk_i)
   begin
      if falling_edge(clk_i) then
         f_falling_rd_en   <= rd_en_i;
         f_falling_rd_addr <= rd_addr_i;
      end if;
   end process p_falling;

   -- Here we make sure that the input signals only transition on the rising
   -- clock edge, not on the falling clock edge.
   assume always {not clk_i} |-> {f_falling_rd_en = rd_en_i and f_falling_rd_addr = rd_addr_i};


   ----------------------------------------------
   -- COVER STATEMENTS TO VERIFY REACHABILITY
   ----------------------------------------------

   -- Useful for debugging, to make sure that the rd_en_i input signal is able to toggle.
   f_toggle : cover {rd_en_i = '1'; rd_en_i = '0'; rd_en_i = '1'};


   ----------------------------------------------
   -- ADDITIONAL ASSERTS NEEDED FOR K-INDUCTION
   ----------------------------------------------


   ----------------------------------------------
   -- ADDITIONAL ASSUMES HELPFUL WHEN DEBUGGING
   ----------------------------------------------


} -- vunit i_registers(registers(synthesis))

